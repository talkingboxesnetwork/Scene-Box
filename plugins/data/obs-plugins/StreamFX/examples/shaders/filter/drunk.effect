// Always provided by OBS
uniform float4x4 ViewProj<
	bool visible = false;
	string name = "View Projection Matrix";
>;

// Provided by Stream Effects
uniform float4 Time<
	bool visible = false;
	string name = "Time Array";
	string description = "A float4 value containing the total time, rendering time and the time since the last tick. The last value is a random number between 0 and 1.";
>;
uniform float4x4 Random<
	bool visible = false;
	string name = "Random Array";
	string description = "A float4x4 value containing random values between 0 and 1";
>;
uniform texture2d ImageSource<
	bool visible = false;
	string name = "Source Texture (Filter, Transition)";
>;
uniform float2 ImageSource_Size<
	bool visible = false;
	string name = "Source Texture Size (Filter, Transition)";
>;
uniform float2 ImageSource_Texel<
	bool visible = false;
	string name = "Source Texture Texel Size (Filter, Transition)";
>;

// Shader Parameters
uniform float p_drunk_strength<
	bool visible = true;
	string name = "Strength";
	float minimum = 0.;
	float maximum = 100.;
	float step = .01;
> = 25.0;
uniform float p_drunk_speed<
	bool visible = true;
	string name = "Speed";
	float minimum = 0.;
	float maximum = 100.;
	float step = .01;
> = 2.0;

// ---------- Shader Code
sampler_state def_sampler {
	AddressU  = Clamp;
	AddressV  = Clamp;
	Filter    = Linear;
};

struct VertData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

struct FragData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

FragData VSDefault(VertData v_in) {
	FragData vert_out;
	vert_out.pos = mul(float4(v_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv  = v_in.uv;
	return vert_out;
}

// ---------- Fixed Color 
#define MAX_PTS 5
#define MAX_LINE 5.

float random_time_at(int x, int y) {
	const float ts[MAX_PTS + 1][MAX_PTS + 1] = {
		{.2, .8, -.2, .452, -.2832, .8},
		{-.28, -1, -.42, -.89, .72, -.29},
		{.75, .25, .33, .67, .98, .01},
		{-.28, 0.8, -.32, -.189, .11, .84},
		{-.48, 0.1, -.2323, -.555, .421, .23},
		{-.28, 0.3, -1.3333, 1.333, 4, 1},
	};
	
	return ts[x][y];
}

float2 mult_at(int x, int y) {
	float x2 = fmod(x, 2.);
	float y2 = fmod(y, 2.);
	
	float2 mult;
	mult.x = (x2 < 1.) ? -1. : 1.;
	mult.y = (y2 < 1.) ? -1. : 1.;
	
	return mult;
}

float4 PS_Drunk(FragData v_in) : TARGET {
	float2 uvs[MAX_PTS + 1][MAX_PTS + 1];	
	for (int x = 0; x <= MAX_PTS; x++) {
		for (int y = 0; y <= MAX_PTS; y++) {
			float2 off = float2(0, 0);
			if ((x > 0) && (x < MAX_PTS)) {
				off.x = cos(Time.y * p_drunk_speed + random_time_at(x, y)) * ImageSource_Texel.x;
			}
			if ((y > 0) && (y < MAX_PTS)) {
				off.y = sin(Time.y * p_drunk_speed + random_time_at(x, y)) * ImageSource_Texel.y;
			}
			off *= (p_drunk_strength / 100.0) * ImageSource_Size * 0.5 * mult_at(x, y);
				
			uvs[x][y] = float2(x / MAX_LINE + off.x, y / MAX_LINE + off.y);
		}
	}
	
	float2 fade = frac(v_in.uv * MAX_LINE);
	fade = (sin((fade - 0.5) * 3.141) + 1.0) * 0.5;
	
	int2 _low = int2(floor(v_in.uv * MAX_LINE));
	int2 _hig = int2(ceil(v_in.uv * MAX_LINE));
	
	float2 uv = v_in.uv;
	float2 uv_tl = uvs[_low.x][_low.y];
	float2 uv_tr = uvs[_hig.x][_low.y];
	float2 uv_bl = uvs[_low.x][_hig.y];
	float2 uv_br = uvs[_hig.x][_hig.y];
	
	float2 uv_t = lerp(uv_tl, uv_tr, fade.x);
	float2 uv_b = lerp(uv_bl, uv_br, fade.x);
	uv = lerp(uv_t, uv_b, fade.y);
	
	return ImageSource.Sample(def_sampler, uv);
}

technique Draw
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader  = PS_Drunk(v_in); 
	}
}
